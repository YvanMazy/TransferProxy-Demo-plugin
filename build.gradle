group = 'net.transferproxy'
version = '1.1.0'

buildscript {
    repositories {
        mavenCentral()
    }
}

allprojects {
    apply plugin: 'java'

    group = 'net.transferproxy'
    version = rootProject.version

    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        compileOnly 'com.github.YvanMazy.TransferProxy:api:1.1.0'
    }

    java {
        toolchain {
            languageVersion.set(JavaLanguageVersion.of(17))
        }
    }

    processResources {
        def props = [version: version]
        inputs.properties props
        filteringCharset 'UTF-8'
        filesMatching('plugin.yml') {
            expand props
        }
    }
}

task buildAllJars(type: Copy) {
    into layout.buildDirectory.dir("libs")

    subprojects {
        afterEvaluate { project ->
            if (project.plugins.hasPlugin('java')) {
                if (project.plugins.hasPlugin('com.github.johnrengelman.shadow')) {
                    from project.tasks.named('shadowJar')
                } else {
                    from project.tasks.named('jar')
                }
            }
        }
    }
}